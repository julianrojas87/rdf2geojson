PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX osmt: <https://wiki.openstreetmap.org/wiki/Key:>
PREFIX osmm: <https://www.openstreetmap.org/meta/>
PREFIX geof: <http://www.opengis.net/def/geosparql/function/>
PREFIX geojson: <https://purl.org/geojson/vocab#>

CONSTRUCT {
  <http://example.org/data> a geojson:FeatureCollection;
        geojson:features ?osmid.

    ?osmid a geojson:Feature;
        geojson:geometry [ 
            a geojson:Point;
            geojson:coordinates [ 
                rdf:first ?long;
                rdf:rest [
                    rdf:first ?lat;
                    rdf:rest rdf:nil
                ] 
            ]
        ]
} WHERE {
  
  ?osmid osmm:type "n" ;
         osmt:highway ?roadType ;
         osmm:loc ?geom .
  
  BIND(xsd:double(REPLACE(str(?geom), "^[^0-9\\.-]*([-]?[0-9\\.]+) .*$", "$1" )) AS ?long)
  BIND(xsd:double(REPLACE(str(?geom), "^.* ([-]?[0-9\\.]+)[^0-9\\.]*$", "$1" )) AS ?lat)
  # Filter for nodes within 2km from the center of Leuven
  BIND(geof:distance(?geom, "POINT(4.70117 50.87940)"^^geo:wktLiteral) AS ?distance)
  FILTER(?distance < 2)
}
LIMIT 20